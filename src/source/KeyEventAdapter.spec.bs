import "pkg:/source/KeyEventAdapter.bs"

namespace tests
    @async
    @SGNode("TestNode")
    @suite("[KEA] KeyEventAdapterTests")
    class KeyEventAdapterTests extends rooibos.BaseTestSuite

        '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
        @describe("test key event adapter")
        '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

        @beforeEach
        function _()
            ' reset test fields
            m.node.top.key = ""
            m.node.top.press =  false
            m.node.top.longPress = false 
        end function

        @it("get long press timeout")
        function _()
            duration = keyevent.getLongPressTimeout()
            b = rooibos.common.isNumber(duration)
            m.assertTrue(b, "getLongPressTimeout should return a number")
        end function

        @it("test key press")
        function _()
            keyName = "TheKey"
            isPressed = true
            m.assertTrue(keyName <> m.top.key)
            onKeyEvent(keyName, isPressed) 'bs:disable-line
            m.assertTrue(keyName = m.top.key and isPressed = m.top.press)
        end function

        @async
        @it("long press test")
        function _()
            m.timer = createObject("rosgnode", "timer")
            m.timer.observeField("fire", (sub()
                ' step 2: release back button
                onKeyEvent("back", false) 'bs:disable-line

                m.testSuite.assertTrue(m.testSuite.node.top.key = "back")
                m.testSuite.assertFalse(m.testSuite.node.top.press)
                m.testSuite.assertTrue(m.testSuite.node.top.longPress)
                m.testSuite.done()
            end sub).tostr().split(" ")[1])

            ' step 1: press back button and wait
            onKeyEvent("back", true) 'bs:disable-line
            m.timer.control = "start"
        end function

        @it("short press test")
        function _()
            ' step 1: press play button
            onKeyEvent("play", true) 'bs:disable-line
            ' step 2: release play button
            onKeyEvent("play", false) 'bs:disable-line

            m.assertTrue(m.node.top.key = "play")
            m.assertFalse(m.node.top.press)
            m.assertFalse(m.node.top.longPress)
        end function

    end class
end namespace
