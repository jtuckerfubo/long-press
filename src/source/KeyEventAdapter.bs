namespace keyevent

const LONG_PRESS_DURATION = 500 'milliseconds

function getLongPressTimeout() as integer
    return LONG_PRESS_DURATION
end function

' determines if a key event is a long press or not
' long presses are detected on the button release (press = false) 
'
' suggested usage for rc keys in specific node instances where you care about long press detection:
' 1. can be used narrowly in specific nodes
' 2. can be used narrowly with specific rc keys
' 3. call this at the top of onKeyEvent() for true and false press states
' 4. when press = true, return true for these specific rc keys 
' see Region.onKeyEvent() in example project
' 
' returns true when a long press is detected, otherwise false
function isLongPress(key as string, press as boolean) as boolean
    datetime = createObject("roDateTime")
    seconds& = datetime.asSeconds() * 1000&
    millis& = datetime.getMilliseconds()
    time = seconds& + millis&
    if press then
        m.__key_press_time = time
        m.__key_pressed = key
    else
        prevkey = m.__key_pressed
        m.__key_pressed = ""
        if prevkey = key then
            if time - LONG_PRESS_DURATION >= m.__key_press_time then
                return true
            end if
        end if
    end if
    return false
end function

end namespace